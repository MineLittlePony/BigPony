buildscript {
    dependencies {
        classpath 'com.github.dexman545:Outlet:1.3.10'
    }
}
plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
    id 'org.ajoberstar.reckon' version '0.13.0'
}
apply plugin: 'dex.plugins.outlet'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

outlet.allowSnapshotsForProject = false
outlet.mcVersionRange = project.minecraft_version_range

group = project.group
description = project.displayname
archivesBaseName = project.name

loom {
    mixin.defaultRefmapName = 'bigpony.mixin.refmap.json'
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

repositories {
    maven { name 'modmenu'; url 'https://maven.terraformersmc.com/releases' }
    maven { name 'minelp'; url 'https://repo.minelittlepony-mod.com/maven/snapshot' }
    maven { name 'minelp-release'; url 'https://repo.minelittlepony-mod.com/maven/release' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modApi "net.fabricmc:fabric-loader:${project.loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi "com.minelittlepony:minelittlepony:${project.minelp_version}"

    modApi "com.minelittlepony:kirin:${project.kirin_version}"
    include "com.minelittlepony:kirin:${project.kirin_version}"
    
    modCompileOnly "com.minelittlepony:hdskins:${project.hd_skins_version}"
    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
    inputs.property "version", project.version.toString()

    filesMatching("fabric.mod.json") {
        expand "version": project.version.toString(),
                "minecraftVersion": project.minecraft_version_range,
                "loaderVersion": ">=${project.loader_version}",
                "fabricVersion": ">=${project.fabric_version}",
                "hdSkinsVersion": ">=${project.hd_skins_version}",
                "kirinVersion": ">=${project.kirin_version}"
    }

    from 'LICENSE'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
tasks.modrinth {
    onlyIf {
      def stage = version.version.stage.value
      stage == null || stage.name != 'beta'
    }
}

modrinth {
    token = System.env.MODRINTH_KEY
    projectId = project.modrinth_project_id
    //featured = true
    versionNumber = version.toString()
    versionName = archivesBaseName + '-' + version
    changelog = "[Changelog](https://github.com/MineLittlePony/BigPony/releases/tag/${version.toString()})"
    loaders = ['fabric', 'quilt']
    uploadFile = remapJar
    outlet.mcVersions().each{ver ->
      gameVersions.add ver
    }
    dependencies {
       required.project 'P7dR8mSH'
       optional.project 'JBjInUXM'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "BigPony"
                description = "Ahm a big pony now!"
                url = "http://minelittlepony-mod.com"
                licenses {
                    license {
                        name = "MIT Public License"
                        url = "https://tlo.mit.edu/learn-about-intellectual-property/software-and-open-source-licensing"
                    }
                }
                developers {
                    developer {
                        id = "killjoy1221"
                        name = "Matthew Messinger"
                        email = "mattmess1221@gmail.com"
                    }
                    developer {
                        id = "sollace"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/MineLittlePony/BigPony.git'
                    developerConnection = 'scm:git:ssh://github.com/MineLittlePony/BigPony.git'
                    url = 'https://github.com/MineLittlePony/BigPony'
                }
            }
        }
    }

    repositories {
        maven {
            name = "MineLittlePony"
            def stage = version.version.stage.value
            url = "s3://repo.minelittlepony-mod.com/maven/${stage != null && stage.name == 'beta' ? 'snapshot' : 'release'}"
            credentials(AwsCredentials) {
                accessKey = System.env.ACCESS_KEY
                secretKey = System.env.SECRET_KEY
            }
        }
    }
}
